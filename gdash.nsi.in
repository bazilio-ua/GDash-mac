; GDash.nsi
;

;--------------------------------

; The name of the installer
Name "GDash"

!define VERSION GDASH-VERSION
!define MINGWDIR c:\mingw

; The file to write
OutFile "gdash-${VERSION}-installer.exe"

; The default installation directory
InstallDir $PROGRAMFILES\GDash

; Registry key to check for directory (so if you install again, it will 
; overwrite the old one automatically)
InstallDirRegKey HKLM "Software\GDash" "Install_Dir"

;--------------------------------

; Pages
Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

;--------------------------------

; The stuff to install
Section "GDash (required)"

  SectionIn RO
  
  ; Program files
  SetOutPath $INSTDIR
  File "src\gdash.exe"
  File "src\sdash.exe"
  File "src\sreplay.exe"
  File "include\boulder_rush.png"
  File "include\c64_gfx.png"
  File "include\gdash_screen.png"
  File "include\gdash_tile.png"
  File "gdash.ico"
  File /oname=README.txt "README"
  File /oname=TODO.txt "TODO"
  File /oname=COPYING.txt "COPYING"
  File /oname=COPYING_SDL.txt "COPYING.SDL"
  File /oname=COPYING_GTK.txt "COPYING.GTK"
  File README-SDL.txt

  ; Program files, Glib dlls
  SetOutPath $INSTDIR
  File ${MINGWDIR}\bin\intl.dll
  File ${MINGWDIR}\bin\libgio-2.0-0.dll
  File ${MINGWDIR}\bin\libglib-2.0-0.dll
  File ${MINGWDIR}\bin\libgmodule-2.0-0.dll
  File ${MINGWDIR}\bin\libgobject-2.0-0.dll
  File ${MINGWDIR}\bin\libgthread-2.0-0.dll
  
  ; Program files, GTK+ dlls
  SetOutPath $INSTDIR
  File ${MINGWDIR}\bin\freetype6.dll
  File ${MINGWDIR}\bin\libatk-1.0-0.dll
  File ${MINGWDIR}\bin\libcairo-2.dll
  File ${MINGWDIR}\bin\libexpat-1.dll
  File ${MINGWDIR}\bin\libfontconfig-1.dll
  File ${MINGWDIR}\bin\libgdk-win32-2.0-0.dll
  File ${MINGWDIR}\bin\libgdk_pixbuf-2.0-0.dll
  File ${MINGWDIR}\bin\libgtk-win32-2.0-0.dll
  File ${MINGWDIR}\bin\libpango-1.0-0.dll
  File ${MINGWDIR}\bin\libpangocairo-1.0-0.dll
  File ${MINGWDIR}\bin\libpangoft2-1.0-0.dll
  File ${MINGWDIR}\bin\libpangowin32-1.0-0.dll
  File ${MINGWDIR}\bin\libpng12-0.dll
  File ${MINGWDIR}\bin\zlib1.dll
  File ${MINGWDIR}\lib\gtk-2.0\2.10.0\loaders\libpixbufloader-png.dll
  File ${MINGWDIR}\lib\gtk-2.0\2.10.0\loaders\libpixbufloader-jpeg.dll
  File ${MINGWDIR}\lib\gtk-2.0\2.10.0\loaders\libpixbufloader-bmp.dll
  File ${MINGWDIR}\lib\gtk-2.0\2.10.0\loaders\libpixbufloader-gif.dll
  SetOutPath $INSTDIR\etc\gtk-2.0
  File gdk-pixbuf.loaders
  
  ; Program files, SDL dlls
  SetOutPath $INSTDIR
  File ${MINGWDIR}\bin\SDL.dll
  File ${MINGWDIR}\bin\SDL_mixer.dll
  File ${MINGWDIR}\bin\SDL_image.dll
  File ${MINGWDIR}\bin\jpeg.dll
  File ${MINGWDIR}\bin\libvorbis-0.dll
  File ${MINGWDIR}\bin\libvorbisfile-3.dll
  File ${MINGWDIR}\bin\libogg-0.dll


  ; Documentation
  SetOutPath $INSTDIR
  File docs\replay_to_avi.html
  File docs\style.css  
  File docs\background.png
  File docs\gdash.png
  File docs\avidemux_codec.png
  File docs\avidemux_fps.png

  ; Write the installation path into the registry
  WriteRegStr HKLM SOFTWARE\GDash "Install_Dir" "$INSTDIR"
  
  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\GDash" "DisplayName" "GDash"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\GDash" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\GDash" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\GDash" "NoRepair" 1
  WriteUninstaller "uninstall.exe"
  
SectionEnd

; Optional section (can be disabled by the user)
; The stuff to install
Section "Cave sets"

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR\caves
  
  ; Put files there
  File /r /x Makefile* /x create_makefile.sh "caves\*"

SectionEnd

Section "Sound files"
  SetOutPath $INSTDIR\sound
  File "sound\*.ogg"

  SetOutPath $INSTDIR\music
  File "music\*.ogg"

SectionEnd

Section "Translations"
  ; Translations
  SetOutPath $INSTDIR\hu\LC_MESSAGES
  File /r ${MINGWDIR}\share\locale\hu\LC_MESSAGES\gtk20.mo
  File /r ${MINGWDIR}\share\locale\hu\LC_MESSAGES\glib20.mo
  SetOutPath $INSTDIR\de\LC_MESSAGES
  File /r ${MINGWDIR}\share\locale\de\LC_MESSAGES\gtk20.mo
  File /r ${MINGWDIR}\share\locale\de\LC_MESSAGES\glib20.mo

  SetOutPath $INSTDIR\hu\LC_MESSAGES
  File /oname=gdash.mo "po\hu.gmo"
  SetOutPath $INSTDIR\de\LC_MESSAGES
  File /oname=gdash.mo "po\de.gmo"
SectionEnd


; Optional section (can be disabled by the user)
Section "Start Menu Shortcuts"

  SetOutPath $INSTDIR
  CreateDirectory "$SMPROGRAMS\GDash"
  CreateShortCut "$SMPROGRAMS\GDash\GDash.lnk" "$INSTDIR\gdash.exe" "" "$INSTDIR\gdash.ico" 0
  CreateShortCut "$SMPROGRAMS\GDash\GDash (C64-like).lnk" "$INSTDIR\sdash.exe" "" "$INSTDIR\gdash.ico" 0
  CreateShortCut "$SMPROGRAMS\GDash\Replay converter.lnk" "$INSTDIR\sreplay.exe" "" "" 0
  CreateShortCut "$SMPROGRAMS\GDash\README.lnk" "$INSTDIR\README.txt" "" "" 0
  CreateShortCut "$SMPROGRAMS\GDash\Replay to AVI.lnk" "$INSTDIR\replay_to_avi.html" "" "" 0
  CreateShortCut "$SMPROGRAMS\GDash\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  
SectionEnd



; Optional section (can be disabled by the user)
Section "Register *.bd and *.gds files"

  WriteRegStr HKCR ".bd" "" "GDash.Caveset"
  WriteRegStr HKCR ".gds" "" "GDash.Caveset"

  WriteRegStr HKCR "GDash.Caveset" "" "GDash Caveset"
  WriteRegStr HKCR "GDash.Caveset\DefaultIcon" "" "$INSTDIR\gdash.ico,0"

  WriteRegStr HKCR "GDash.Caveset\shell\open\command" "" '"$INSTDIR\gdash.exe" "%1"'
  WriteRegStr HKCR "GDash.Caveset\shell\edit\command" "" '"$INSTDIR\gdash.exe" -e "%1"'
  
SectionEnd



; Optional section (can be disabled by the user)
Section "any2gdash conversion utility"

  SetOutPath $INSTDIR
  File "util\any2gdash.exe"
  
SectionEnd





;--------------------------------
; Uninstaller

Section "Uninstall"
  
  ; Remove registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\GDash"
  DeleteRegKey HKLM SOFTWARE\GDash
  DeleteRegKey HKCR ".bd"
  DeleteRegKey HKCR ".gds"
  DeleteRegKey HKCR "GDash.Caveset"

  ; Remove shortcuts, if any
  Delete "$SMPROGRAMS\GDash\*.*"
  RMDir "$SMPROGRAMS\GDash"

  ; Remove directories used
  RMDir /r "$INSTDIR"

SectionEnd

